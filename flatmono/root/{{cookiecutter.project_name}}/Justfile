@default:
    just --list

######
## CICD Integrations
######

{% raw %}
@pr ASSETTYPE:
    git diff --name-only origin/${GITHUB_BASE_REF} HEAD | grep "^{{ASSETTYPE}}/.*" | cut -d "/" -f2 | uniq | xargs -I {} sh -c 'if [ -d {{ASSETTYPE}}/{} ]; then just {{ASSETTYPE}}/{}/pr; fi'

@register ASSETTYPE:
    git diff --name-only HEAD^1 HEAD -G"^VERSION:=" "{{ASSETTYPE}}/*/Justfile" | cut -d "/" -f2 | uniq | xargs -I {} sh -c 'if [ -d {{ASSETTYPE}}/{} ]; then just {{ASSETTYPE}}/{}/register; fi'

@githook HOOKTYPE:
    # This is an imperfect heuristic for the set of projects you want to invoke hooks in.
    git diff --name-only origin/main HEAD | grep -E "^(images|packages|infra)/.*" | cut -d "/" -f1,2 | uniq | xargs -I {} sh -c 'if [ -d {} ]; then just {}/githook {{HOOKTYPE}}; fi'
{% endraw %}


######
## ODLC Root setup block
######

@massupdate:
    ls -d */*/ | xargs -I {} sh -c 'cd {}; cruft update -y && git stash; cd ../..'
    while git stash list | grep -q 'stash@'; do git stash pop; done

[macos]
@setup:
    echo "Add mac installation scripts here."

[linux]
@setup:
    #!/usr/bin/env bash
    set -euxo pipefail

    #####
    ### This script is debian-oriented.  TODO: more distros
    #####
    sudo apt-get install -y apt-transport-https ca-certificates gnupg
    sudo snap install scc

    #####
    ### docker local dev setup
    #####
    if ! command -v docker &> /dev/null
    then
        curl -fsSL https://get.docker.com -o /tmp/get-docker.sh
        sudo sh /tmp/get-docker.sh
        sudo groupadd docker
        sudo usermod -aG docker $USER
    else
        echo "Docker already installed"
    fi

    if ! command -v hadolint &> /dev/null
    then
        sudo curl -L https://github.com/hadolint/hadolint/releases/download/v2.12.0/hadolint-Linux-x86_64 -o /usr/bin/hadolint
        sudo chmod +x /bin/hadolint
    else
        echo "Hadolint already installed"
    fi


    #####
    ### gcloud local dev setup
    #####
    if ! command -v gcloud &> /dev/null
    then
        echo "deb [signed-by=/usr/share/keyrings/cloud.google.gpg] https://packages.cloud.google.com/apt cloud-sdk main" | sudo tee -a /etc/apt/sources.list.d/google-cloud-sdk.list
        curl https://packages.cloud.google.com/apt/doc/apt-key.gpg | sudo apt-key --keyring /usr/share/keyrings/cloud.google.gpg add -
        sudo apt-get update && sudo apt-get install google-cloud-cli
        gcloud init
        gcloud auth application-default login
        gcloud auth configure-docker
    else
        echo "gcloud already installed"
    fi


    #####
    ### Terraform local dev setup
    #####
    if ! command -v terraform &> /dev/null
    then
        wget -O- https://apt.releases.hashicorp.com/gpg | gpg --dearmor | sudo tee /usr/share/keyrings/hashicorp-archive-keyring.gpg
        echo "deb [signed-by=/usr/share/keyrings/hashicorp-archive-keyring.gpg] https://apt.releases.hashicorp.com $(lsb_release -cs) main" | sudo tee /etc/apt/sources.list.d/hashicorp.list
        sudo apt update
        sudo apt-get install terraform
    else
        echo "terraform already installed"
    fi

    #####
    ### python toolchain local dev setup
    #####
    if ! command -v pyenv &> /dev/null
    then
        apt-get update && apt-get install -y --no-install-recommends make build-essential python3-dev liblzma-dev zlib1g-dev libbz2-dev libreadline-dev libsqlite3-dev libssl-dev libffi-dev
        curl -L https://github.com/pyenv/pyenv-installer/raw/master/bin/pyenv-installer | bash
        echo 'export PYENV_ROOT="$HOME/.pyenv"' >> ~/.bashrc
        echo 'command -v pyenv >/dev/null || export PATH="$PYENV_ROOT/bin:$PATH"' >> ~/.bashrc
        echo 'eval "$(pyenv init -)"' >> ~/.bashrc
        pyenv install 3.10.6 && pyenv global 3.10.6 && pyenv rehash
    else
        echo "pyenv already installed"
    fi

    if ! command -v pipenv &> /dev/null
    then
        pip install --user pipx
        pipx ensurepath
        pipx install pipenv
    else
        echo "pipenv already installed"
    fi
